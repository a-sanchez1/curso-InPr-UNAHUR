program {
  EntrenarAlBeto()
}

procedure EntrenarAlBeto() {
  repeat(4) {
    PatearYVolverAlBordeOeste()
    MoverAlBeto(Norte)
  }
  PatearYVolverAlBordeOeste()
}

procedure MoverAlBetoHastaLaPelota() {
  while (not (llegueALaPelota())) 
  // repetirHastaQue llegueALaPelota
    { MoverAlBeto(Este) }
}

procedure VolverAlBetoAlBordeOeste() {
  while (puedeMover(Oeste)) 
  // En bloques: "repetirHastaQue no puedeMover(Oeste)"
    { MoverAlBeto(Oeste) }
}

procedure PatearLaPelota() {
  MoverElPieDelBeto()
  MoverLaPelotaYVolverABeto()
}

procedure PatearYVolverAlBordeOeste() {
  MoverAlBetoHastaLaPelota()
  PatearLaPelota()
  VolverAlBetoAlBordeOeste()
}

procedure MoverLaPelotaYVolverABeto() {
  while (not (llegueAlArco())) 
  // En bloques: "repetirHastaQue llegueAlArco"
    { MoverLaPelota(Este) }
  while (not (llegueAlBeto())) 
  // En bloques: "repetirHastaQue llegueAlBeto"
    { Mover(Oeste) }
}

function llegueAlBeto()    { return (nroBolitas(Azul)  == 1) }
function llegueAlArco()    { return (nroBolitas(Rojo)  == 1) }
function llegueALaPelota() { return (nroBolitas(Negro) == 1) }

procedure MoverAlBeto(direcciónDelBeto) {
  SacarAlBeto()
  Mover(direcciónDelBeto)
  PonerAlBeto()
}

procedure MoverLaPelota(direcciónDeLaPelota) {
  SacarLaPelota()
  Mover(direcciónDeLaPelota)
  PonerLaPelota()
}

procedure PonerAlBeto()   { Poner(Azul) }
procedure SacarAlBeto()   { Sacar(Azul) }
procedure PonerLaPelota() { Poner(Negro) }
procedure SacarLaPelota() { Sacar(Negro) }

procedure MoverElPieDelBeto() {
  Poner(Azul)
  Sacar(Azul)
}
