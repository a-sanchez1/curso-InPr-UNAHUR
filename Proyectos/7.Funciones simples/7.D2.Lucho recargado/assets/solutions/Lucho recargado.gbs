program {
  EncenderLasFilasDeLuces()
}

procedure EncenderLasFilasDeLuces() {
  repeat(5) {
    EncenderUnaFilaYVolver()
    MoverALuchoAlNorte()
  }
  EncenderUnaFilaYVolver()
}

procedure EncenderUnaFilaYVolver() {
  repeat(8) {
    EncenderLuzSiCorresponde()
    MoverALuchoAlEste()
  }
  EncenderLuzSiCorresponde()
  VolverALuchoAlBordeOeste()
}

procedure EncenderLuzSiCorresponde() {
  if (hayLuzApagada()) { EncenderLuz() }
}

procedure MoverALuchoAlNorte() {
  if (puedeMover(Oeste)) {
    BOOM("No estoy al inicio de una fila")
  }
  else {
    Sacar(Negro)
    Mover(Norte)
    Poner(Negro)
  }
}

procedure VolverALuchoAlBordeOeste() {
  repeat(8) { MoverALuchoAlOeste() }
}

procedure MoverALuchoAlEste() {
  Sacar(Negro)
  Mover(Este)
  Poner(Negro)
}

procedure MoverALuchoAlOeste() {
  Sacar(Negro)
  Mover(Oeste)
  Poner(Negro)
}

procedure EncenderLuz() {
  if      (not(hayLuz()))    { BOOM("No hay luz para prender") }
  else if (hayLuzPrendida()) { BOOM("La luz ya está prendida") }
  else 
    { Poner(Azul) }
}

function hayLuzApagada()  { return (nroBolitas(Azul) == 1) }
function hayLuzPrendida() { return (nroBolitas(Azul) == 2) }
function hayLuz()         { return (hayLuzApagada() || hayLuzPrendida()) }
